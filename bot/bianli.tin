#class bianli open;
#read bot/bianli_data.tin;

#var bl_in_action 0;
#alias {bl.setup} {
    #var bl_path ${bl_path_${CurrentPosition}};
    #var bl_connections ${bl_connections_${CurrentPosition}};
};

#alias {bl.command} {
    #alias tmp_command %1;
    tmp_command;
    #delay 0.1 {
        bl.walk;
    };
};
#alias {bl.command_halt} {
    #alias tmp_command %1;
    tmp_command;
};

#alias {bl.clear} {
    on_cleared {
        bl.walk;
    };
    clear %1;
};

#alias {on_cleared} {
    #alias tmp_on_cleared %0;
    #action {clear: done} {
        #unaction {clear: done};
        #delay {3} {
            tmp_on_cleared;
        };
    };
};
#alias war_prepare {
    yun recover;
    yun regenerate;
    yun powerup;
};

#gag clear: done;
#alias {clear} {
    war_prepare;
    kill %0;
    #class clear.inner open;
    #action {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        #nop raw;
        #showme clear: done;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};
#alias {clear_all} {
    war_prepare;
    killall %0;
    #class clear.inner open;
    #action {^这里没有这个人} {
        #unaction {^这里没有这个人};
        #class clear.inner kill;
        #nop raw;
        #showme clear: done;
    } {1};
    #ticker {kill} {
        kill %0;
    } {3};
    #class clear.inner close;
};

#alias {bl.clear_force} {
    on_cleared {
        bl.walk;
    };
    clear %1;
};
#alias {bl.clear_all} {
    on_cleared {
        bl.walk;
    };
    clear_all %1;
};

#alias {bl.stop} {
    #var critical_running 0;
    bl.pause;
    #class bl.start.inner kill;
};

#alias {bl.pause} {
    unset brief;
    #class bl.action.inner kill;
    #class bl.inner kill;
    #unticker bl.action.inner.tic;
    #undelay {bl.delay};
};

#var bl_path_index 0;

#alias {bl.continue} {
    #alias {bl.back} {
        bl.merge;
        bl.continue;
    };
    #class bl.inner open;
    #action {这里没有任何明显的出路} {
        #if {${bl_in_action} == 0} {
            #delay {bl.delay} {
                bl.walk;
            } {0.05};
        };
    } {1};
    #action {^%s这里%*的{出口|方向}{是|有}%*$} {
        #showme <afa> 进度~[$bl_path_index / $bl_path_size];
        #if {${bl_in_action} == 0} {
            #delay {bl.delay} {
                bl.walk;
            } {0.1};
        }; 
    };
    #action {你的动作还没有完成，不能移动。} {
        #math bl_path_index ${bl_path_index}-1;
        bl.wait 1;
    };
    #action {你逃跑失败。} {
        #math bl_path_index ${bl_path_index}-1;
        bl.truce;
    };
    #action {%*几乎没有路了，你走不了那么快。} {
        #math bl_path_index ${bl_path_index}-1;
        bl.wait 1;
    };
    #action {^{你忽然不辨方向，不知道该往哪里走了。|你小心翼翼往前挪动，遇到艰险难行处，只好放慢脚步。|你小心翼翼往前挪动，生怕一不在意就跌落山下。|你还在山中跋涉，一时半会恐怕走不出这藏边群山！|青海湖畔美不胜收，你不由停下脚步，欣赏起了风景。|你还在山中跋涉，一时半会恐怕走不出这西南地绵绵群山！|你还在山中跋涉，一时半会恐怕走不出这六盘山！|山路崎岖，你不得不停下来歇歇脚。}} {
        #math bl_path_index ${bl_path_index}-1;
        bl.wait 1;
    };
    #action {^此去往东是荒郊野岭} {
        #math bl_path_index ${bl_path_index}-1;
        bl.wait 1;
    };
    #action {^打手们把门关了起来,黑老板嘿嘿笑了几声......} {
        bl.walk;
    };
    #action {^你从上面爬了下来，衣服都烂了，看起来十分狼狈。} {
        #delay 0.5 {bl.walk}
    };
    #action {^你从下面爬了上来，衣服都烂了，看起来十分狼狈。} {
        #delay 0.5 {bl.walk}
    };
    #action {^东面是一面悬崖，荆棘遍布，山腰似乎有个洞(hole)。} {
        #delay 0.5 {bl.walk}
    };
    #action {^那个洞黑乎乎的，不知道有什么。} {
        #delay 0.5 {bl.walk}
    };

    #action {^跑堂说道：「林知府府上预订了席面，我们这实在走不开，你可以帮我送去吗？} {
        give 1 gold to pao tang;
        look;
    };
    #action {^走路太快，你没在意脚下，被杂草绊了一下。} {
        #math bl_path_index ${bl_path_index}-1;
        bl.wait 1;
    };


    #alias {bl.walk} {
        #var bl_in_action 0;
        #math bl_path_index ${bl_path_index}+1;
        #if {${bl_path_index} >= ${bl_path_size}} {
            bl.pause;
            #list bl_path create {};
            #showme 遍历结束;
        };
        #else {
            #list bl_path get ${bl_path_index} bl_path_next;
            #if {@is_bl_action{${bl_path_next}} == 1} {
                #var bl_in_action 1;
            };
            ${bl_path_next};
        };
    };

    #action {这个方向没有出路} {
        bl.stop;
    };

    #if {"%1" != ""} {
        #action {%1} {
            bl.stop;
            #showme 找到目标: %1;
        } {1};
    };
    #class bl.inner close;
    bl.walk;
};

#alias {bl.start} {
    set brief 2;
    #var critical_running 1;
    #list bl_connections create {};
    bl.setup;

    #class bl.start.inner open;
    #var original_here ${CurrentPosition};
    #var connections ${bl_connections};
    #var connections_index 0;
    #list connections size connections_count;

    #action {遍历结束} {
        #showme bianli: done;
        bl.stop;
    };
    #class bl.start.inner close;
    bl.bianli %1;
};

#alias {bl.bianli} {
    #math connections_index ${connections_index}+1;
    #if {${connections_index} > ${connections_count}} {
        #var next_area NULL;
    };
    #else {
        #list connections get ${connections_index} next_area;
    };
    bl.setup;
    #list bl_path size bl_path_size;
    #var bl_path_index 0;
    bl.continue %1;
};

#alias {bl.go} {
    #list bl_path size bl_path_size;
    #var bl_path_index 0;
    bl.continue;
};

#alias {bl.truce} {
    #show <afa> in bl.truce;
    on_truce {
        bl.walk;
    };
};
#alias {on_truce} {
    #show <afa> in on_truce;
    #alias tmp_truce %0;
    #show <afa> tmp_truce :%0;
    #class truce.inner open;
    #action {truce} {
        #class truce.inner kill;
        on_unbusy {
            tmp_truce;
        };
    };
    #class truce.inner close;
    check_health;
};
#alias check_health {
    #class check_health.inner open;
    #action {你现在气力充沛} {
        #showme truce;
        #untic xq1;
        #class check_health.inner kill;
    };
    #class check_health.inner close;
    #tic {xq1} {yun recover} {0.5};
};

#alias {bl.wait} {
    #delay {%1} {
        bl.walk;
    };
};

#alias {bl.merge} {
    #list tmp_path create;
    #list tmp_path add #cr;
    #var tmp_index 1;

    #math bl_path_index ${bl_path_index}+1;
    #while {${bl_path_index} <= ${bl_path_size}} {
        #list bl_path get ${bl_path_index} v;
        #list tmp_path get ${tmp_index} tmp_top;
        mergeable_bl ${v} ${tmp_top};
        #if {${merge_result} == 1} {
            #list tmp_path delete $tmp_index;
            #math tmp_index ${tmp_index}-1;
        };
        #else {
            #list tmp_path add ${v};
            #math tmp_index ${tmp_index}+1;
        };
        #math bl_path_index ${bl_path_index}+1;
    };
    #var bl_path ${tmp_path};
    #var bl_path_index 1;
    #list bl_path size bl_path_size;
};

#alias {mergeable_bl} {
    #if {"%1" == "n" && "%2" == "s"
    ||"%1" == "s" && "%2" == "n"
    ||"%1" == "w" && "%2" == "e"
    ||"%1" == "e" && "%2" == "w"
    ||"%1" == "se" && "%2" == "nw"
    ||"%1" == "nw" && "%2" == "se"
    ||"%1" == "sw" && "%2" == "ne"
    ||"%1" == "ne" && "%2" == "sw"
    ||"%1" == "u" && "%2" == "d"
    ||"%1" == "d" && "%2" == "u"
    ||"%1" == "nu" && "%2" == "sd"
    ||"%1" == "sd" && "%2" == "nu"
    ||"%1" == "su" && "%2" == "nd"
    ||"%1" == "nd" && "%2" == "su"
    ||"%1" == "eu" && "%2" == "wd"
    ||"%1" == "wd" && "%2" == "eu"
    ||"%1" == "wu" && "%2" == "ed"
    ||"%1" == "ed" && "%2" == "wu"
    ||"%1" == "enter" && "%2" == "out"
    ||"%1" == "out" && "%2" == "enter"}
    {
        #var merge_result 1;
    };
    #else {
        #var merge_result 0;
    };
};

#alias {bl.test} {
    #list bl_path create {
    };
    #list bl_path size bl_path_size;
    #var bl_path_index 0;
    bl.continue "nop";
};

#alias {bl.comment} {
    #echo {%c%h} {light orange} %1;
    bl.walk;
};

#function {blsupport} {
    #list bl_path_%1 size tmp;
    #if {${tmp} == 0} {
        #return 0;
    };
    #else {
        #return 1;
    };
};

#alias {bl.action.guolin} {
    #class bl.action.inner open;
    #ticker {bl.action} {
        w;e
    } {0.8};
    #action {后山} {
        #class bl.action.inner kill;
        s;
        bl.walk;
    };
    #class bl.action.inner close;
};
#alias {bl.action.guolinb} {
    #class bl.action.inner open;
    #ticker {bl.action} {
        w;e;
    } {0.8};
    #action {大道} {
        #class bl.action.inner kill;
        #var bl_action 0;
        s;
        bl.walk;
    };
    #class bl.action.inner close;
};
#alias bl.action.yhb {
  unset brief;
  #class bl.action.inner open;
    #action {%*根本就弄不清自己身在何处。$} {
      #showme <afa> 未找到出口，继续往南;
      #tic bl.action.inner.tic {
        s;
        #untic bl.action.inner.tic;
      } 0.5;
    };
    #action {一条小道通向前方} {
      #showme <afa> 找到出口，继续遍历;
      #class bl.action.inner kill;
      #tic bl.action.inner.tic {
        #untic bl.action.inner.tic;
        n;
        bl.walk;
      } 0.5;
    };
  #class bl.action.inner close;
  s;
};

#function {is_bl_action} {
    #regexp {%1} {^bl.action} {#var result 1} {#var result 0};
};

#alias {on_there} {
    #alias tmp_there #cr;
    #alias tmp_there %0;
    #action {GPS: 抵达目的地} {
        #unaction {GPS: 抵达目的地};
        on_unbusy {
            tmp_there;
        };
    };
};

#alias {on_not_there} {
    #alias tmp_not_there #cr;
    #alias tmp_not_there %0;
    #action {GPS: 目标房间不可达, 无法行走} {
        #unaction {GPS: 目标房间不可达, 无法行走};
        on_unbusy {
            tmp_not_there;
        };
    };
};

#alias {on_unbusy} {
    #list unbusy_list add {%0};
    #show <afa> unbusy_list: $unbusy_list;
    #if {${busying} == 0} {
        do_unbusy;
    };
};

#var busying 0;
#list unbusy_list create {};
#alias {do_unbusy} {
    #list unbusy_list size busy_list_size;
    #if {${busy_list_size} == 0} {
        #var busy_action nil;
    };
    #else {
        #list unbusy_list get ${busy_list_size} busy_action;
        #list unbusy_list delete ${busy_list_size};
        #show <afa> executing action: $busy_action;
    };
    #if {"${busy_action}" != "nil"} {
        #var busying 1;
        on_unbusy_inner {
            ${busy_action};
            do_unbusy;
        };
    };
    #else {
        #var busying 0;
    };
};

#gag {你不忙};
#alias {on_unbusy_inner} {
    #alias tmp_unbusy #cr;
    #alias tmp_unbusy %0;
    #class unbusy.inner open;
    #action {你不忙} {
        #class unbusy.inner kill;
        #undelay {delay_unbusy};
        tmp_unbusy;
    } {1};
    #ticker {checkbusy} {
        checkbusy;
    } {0.5};
    #class unbusy.inner close;
    checkbusy;
};

#class bianli close;
