#nop 任务;
#class hba open;

#var dir {
  {n} {north}
  {s} {south}
  {e} {east}
  {w} {west}
  {nw} {northwest}
  {sw} {southwest}
  {ne} {northeast}
  {se} {southeast}
  {nu} {northup}
  {nd} {northdown}
  {su} {southup}
  {sd} {southdown}
  {eu} {eastup}
  {ed} {eastdown}
  {wu} {westup}
  {wd} {westdown}
  {enter} {enter}
  {out} {out}
  {u} {up}
  {d} {down}
};
#re bot/hb_data.tt;

#ali {hbaa} {
  set wimpy 20;
  #var step 1;
  #var encounter 0;
  #if {$in_bxs==1} {
    ask $master about job;
  };
  #else {getesc %1}
};
#ali {hbd} {ask $master about finish;hbx};
#ali {hbf} {ask $master about fail;#untic hba1;#untic hba2;hbx;};
#ali {t} { 
  #if {"%0"=="ride"||"%0"=="ride north"||"%0"=="ride west"}{%0};
  #elseif {"%0"=="ny1"} {bo xiaolu; gan che to $dir[ne];}; 
  #else { gan che to $dir[%1]; }; };
#act {^${myname}把这批红货送到%*那里，他已经派了个伙计名叫%*到%*附近接你，把镖车送到他那里就行了。} {
  #tic {hba1} {#echo {%c%h} {light yellow} {目的地%1的%3, 伙计%2}} {5}
};
#ali start {
  #var path ${%1};
  #var step 1;
  #var battle 0;
  #var biased 0;
  #var arrive 0;
  #var inheal 0;
  #var npfm 0;
  #var in_power 0;
  #class hb_tmp open;
    #ali g {
      t $path[$step][方向];
    };
    #ali dn {
      #tic yng {
        halt;xq;
        #if {"$myid"=="simontuixue"} {p.yy};
        #elseif {"$myid"=="yrz" && $in_power==0} {yun powerup;};
        #if {$biased==0} {g};
      } {3};
    };
    #act {^你慷慨长啸，苍冥功陡然运起，周遭尽是磅礴之气，众人觉得甚是悲怀壮烈。} {
      #var in_power 1;
      #showme <afa> in_power: $in_power;
    };
    #act {^浩气苍冥功运功完毕，心中壮怀之激烈情绪犹留心中。} {
      #var in_power 0;
      #showme <afa> in_power: $in_power;
    };
    #act {^斜刺里冲出来%*,猛地对你痛下杀手！} {
      on_unbusy {
        p.fh
      }
    };
    #act {^劫匪突然从暗处跳了出来%*} {
      #untic yng;
      #math encounter {$encounter + 1};
      #var battle 1;
      #showme <afa> $battle;
      #var op {$myid's robber};
      on_unbusy {
        hit $op;
        p.fh $op;
        #tic {battle} {
          hit $op;
          p.sc $op;
        } {4};
      }
    };
    #act {劫匪趁你不注意，推着镖车就跑，} {
      #var biased 1;
      #tic {hba2} {#echo {%c%h} {light red} {已偏航，请修正！}} {5};
    };
    #ali adj {
      #var step %%1;
      #showme <afa> 已修正，step:$step;
      #var biased 0;
      #untic hba2;
    };
    #act {( 只听见「啪」地一声，你手中的%*已经断为两截！ )} {
      wi dao;
    };
    #act {你赶着镖车驶了过来} {
      #if {$battle==1} {
        on_unbusy {dazuo 60;}
      };
      #var room $path[$step][房间]; 
      #if {$biased==0} {
        #math step {$step+1};
      };
      #if {$step > &path[]} {
        #var arrive 1;
        mplay pLevelUp.wav;
        #showme <afa> 到达目的地附近！！！;
        #untic yng;
      };
      #echo {%c%h} {light yellow} {遇敌数：$encounter ，你现在走了$step步，应该在$room,实际在%%1}; 
    };
    #act {^你想攻击谁？} {
      #untic {battle};
      #var battle 0;
      eat liang;drink niurou tang;
      yun heal;
      dn;
    };
    #act {你跳上了小船} {
      #math step {$step+1};
    };

    #act {你现在气血充盈} {
      #untic yheal;
      yun recover;
      dn;
    };
    #act {^你从小船上跳了下来%*} {g};
    #act {^你累了个半死，终于把镖运到了地头。} {
      #class hb_tmp kill;
      #untic battle;
      #untic hba1;
      #untic hba2;
      #untic yng;
    };
    #gag {你灵犀一动，招式再变%*};
    #gag {你灵犀一动，招式再变%*};
    #gag {^>%s$};
    #nop #gag {你在攻击中%*};
    #gag {你随手按住劫匪大穴};
  #class hb_tmp close;
  dn
};
#nop #act {你似乎十分疲惫，看来需要好好休息了} { yun recover};
#nop #act {你气喘嘘嘘，看起来状况并不太好} { yun recover};
#nop #act {你看起来已经力不从心了} { yun recover};
#class hba close;
