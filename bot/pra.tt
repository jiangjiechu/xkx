#nop 遍历;
mld;
#class pra open;
#alias pd {
  #showme <afa> 清空路径;
  #var path_tmp {};
};
#alias pn {
  #var in_record 1;
  #var auto_go 1;
  #var path_tmp {};
  #show <afa> 开始录制，当前路径为[$path_tmp];
};
#alias pm {
  #list path_tmp size _n;
  #show <afa> 当前路径为：[$path_tmp]  长度：$_n;
};
#alias ps {
  #showme  <afa> save path_tmp to %1;
  #var %1 $path_tmp;
  #var %1 @squeeze{%1};
  #show %1:;
  #show ${%1};
  #var path_tmp {};
};
#alias pr {
  #list path_tmp add $dir[%1];
};
#alias ph {
  #var auto_go 0;
};
#alias f {
  #var last_dir $dir[%1];
  #list path_tmp add $dir[%1];
  $dir[%1];
};
#alias f2 {
  #list path_tmp add %1;
};
#alias b {
  #showme <afa> 无效方向，回退一格;
  #list path_tmp delete {-1};
};
#action {^%s这里{唯一|明显}的{出口|方向}{有|是}%*} {
  #var dir_out ${gmcp_move[1][dir]};
  #list dir_out size n;
  #if {$n > 2} {
    #showme <afa> 遇到岔路！请选择方向！
  };
  #elseif {$n == 2} {
    #foreach {$dir_out[]} {_dir} {
      #if {"$_dir"!="@oppose{$last_dir}"} {
        #showme <afa> $_dir vs @oppose{$last_dir};
        #var next_dir $_dir;
      }
    };
    #var last_dir $next_dir;
    #if {$auto_go==1}  {
      #delay {0.2} {
        #list path_tmp add $next_dir;
        $next_dir;
      }
    }
  };
  #else {
    #showme <afa> 没有路了！;
  };
};

#var dir {
  {n} {north}
  {s} {south}
  {e} {east}
  {w} {west}
  {nw} {northwest}
  {sw} {southwest}
  {ne} {northeast}
  {se} {southeast}
  {nu} {northup}
  {nd} {northdown}
  {su} {southup}
  {sd} {southdown}
  {eu} {eastup}
  {ed} {eastdown}
  {wu} {westup}
  {wd} {westdown}
  {enter} {enter}
  {out} {out}
  {u} {up}
  {d} {down}
};
#var abr {
  {north} {n}
  {south} {s}
  {east} {e}
  {west} {w}
  {up} {u}
  {down} {d}
};

#var back {
  {north} {south}
  {south} {north}
  {east} {west}
  {west} {east}
  {northeast} {southwest}
  {southeast} {northwest}
  {southwest} {northeast}
  {northwest} {southeast}
  {up} {down}
  {down} {up}
  {northdown} {southup}
  {southdown} {northup}
  {eastdown} {westup}
  {westdown} {eastup}
  {northup} {southdown}
  {southup} {northdown}
  {eastup} {westdown}
  {westup} {eastdown}
  {enter} {out}
  {out} {enter}
};
#function oppose {
  #var result $back[%1]
};
#function squeeze {
  #var result ${%1};
  #list result collapse {;};
  #foreach {*abr[]} {_dir} {
    #replace result {$_dir} {$abr[$_dir]};
  }
};


#class pra close;
